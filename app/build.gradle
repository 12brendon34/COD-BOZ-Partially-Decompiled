plugins {
    id 'com.android.application'
}

android {
    namespace 'com.activision.boz'

    def versionPropsFile = file('version.properties')
    if (!versionPropsFile.canRead()) {
        throw new GradleException("Could not read version.properties!")
    }

    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionPropsFile))

    def code = versionProps['VERSION_CODE'].toInteger() + 1
    versionProps['VERSION_CODE']=code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    defaultConfig {
        minSdk 22
        targetSdk 34
        versionName "1.0.8.1"
        versionNameSuffix 'dec'
        versionCode code
        applicationId 'com.activision.boz'
        signingConfig signingConfigs.debug
        multiDexEnabled false
    }

    aaptOptions {
        noCompress 'dz'
        noCompress 's3e'
    }

    buildTypes {
        debug {
            ndk {
                abiFilters "armeabi-v7a" //until I can figure out how to remove or replace libisdevice.so
            }
        }
        release {
            ndk {
                abiFilters "armeabi-v7a"
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    dependenciesInfo {
        includeInApk true
        includeInBundle true
    }

    lint {
        abortOnError false
    }

    compileSdk 34
    ndkVersion '25.1.8937393'
}

dependencies {
    // some dependencies
    debugImplementation files('src/main/lib/s3e_debug.jar')
    releaseImplementation files('src/main/lib/s3e_release.jar')

    implementation files('src/main/lib/s3e_resources.jar')
    implementation files('src/main/lib/bolts-android-1.1.2.jar')
}
