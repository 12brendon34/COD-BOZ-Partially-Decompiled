plugins {
    id 'com.android.application'
}

android {
    signingConfigs {
        // Stored in gradle.properties
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyPassword RELEASE_KEY_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
        }
    }

    namespace 'com.activision.boz'

    compileSdk 34
    ndkVersion '25.1.8937393'

    def versionPropsFile = file('version.properties')
    if (!versionPropsFile.canRead()) {
        throw new GradleException("Could not read version.properties!")
    }

    def versionProps = new Properties().with {
        load(new FileInputStream(versionPropsFile))
        def currentCode = getProperty('VERSION_CODE').toInteger()
        def newCode = currentCode + 1
        setProperty('VERSION_CODE', newCode.toString())
        store(versionPropsFile.newWriter(), null)
        return delegate
    }

    defaultConfig {
        applicationId 'com.activision.boz'
        minSdk 19
        targetSdk 36
        versionCode versionProps['VERSION_CODE'].toInteger()
        versionName "1.0.8.1"
        versionNameSuffix 'dec'
        multiDexEnabled false
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug

            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a"
            }
        }
        release {
            signingConfig signingConfigs.release

            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a"
            }
            //not worth enabling
            minifyEnabled false
        }
    }

    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11

        tasks.withType(JavaCompile).configureEach {
            options.compilerArgs += ["-Xlint:deprecation"]
        }
    }

    dependenciesInfo {
        includeInApk = true
        includeInBundle = true
    }

    lint {
        abortOnError = false
    }
}

dependencies {
    implementation files('src/main/lib/s3e_resources.jar')
    implementation files('src/main/lib/bolts-android-1.1.2.jar')

    debugImplementation files('src/main/lib/s3e_debug.jar')
    releaseImplementation files('src/main/lib/s3e_release.jar')
}
